#= require jplayer/jquery.jplayer.min
#= require jquery.raty.min

window.SC ||= {}
window.SC.trackEvent = (event, metadata = {}) ->
  metadata.event  = event
  metadata.secret = $('body').data('recipient-id')
  
  if metadata.track
    if window.ga?
      # Track event with Analytics:
      window.ga 'send', 'event', 'player', event, "/tracks/#{metadata.track}"
    
    if metadata.secret?
      # Track event with our own implementation:
      $.post "/track_events", metadata

class SC.Player
  constructor: (@widget, @tracks) ->
    @player      = @widget.find('.jplayer')
    @track       = null
    @playing     = false
    @currentTime = '00:00'
    
    @initializePlayer()
  
  initializePlayer: ->
    @player.jPlayer(
      swfPath:  '<%= asset_path("jplayer/Jplayer.swf") %>'
      supplied: 'mp3'
      volume:   0.8,
      ready: => @change @tracks[0] if @tracks.length >= 1
    ).bind(
      $.jPlayer.event.timeupdate,
      (event) => @updateTimer(event)
    ).bind(
      $.jPlayer.event.waiting,
      (event) => @widget.addClass('player-loading')
    ).bind(
      $.jPlayer.event.playing,
      (event) => @widget.removeClass('player-loading')
    ).bind(
      $.jPlayer.event.ended,
      (event) => @next()
    )
  
  updateTimer: (event) ->
    status = event.jPlayer.status
    @currentTime = status.currentTime
    @widget.find('.current-time').text $.jPlayer.convertTime(@currentTime)
    
    @widget.removeClass('player-loading') if @currentTime > 0
  
  perform: (args...) ->
    @player.jPlayer args...
  
  isPlaying: ->
    @playing
  
  next: ->
    SC.trackEvent 'next-track', track: @track?.attr('data-track-id'), timer: @currentTime
    
    if nextTrack = @tracks[@tracks.index(@track) + 1]
      @change nextTrack
    else
      @change @tracks[0]
    @play()
  
  prev: ->
    SC.trackEvent 'previous-track', track: @track?.attr('data-track-id'), timer: @currentTime
    
    if prevTrack = @tracks[@tracks.index(@track) - 1]
      @change prevTrack
    else
      @change @tracks[@tracks.length - 1]
    @play()
  
  play: (args...) ->
    SC.trackEvent 'play-track', track: @track?.attr('data-track-id'), timer: @currentTime
    
    unless @playing
      @playing = true
      @perform 'play', args...
  
  pause: (args...) ->
    SC.trackEvent 'pause-track', track: @track?.attr('data-track-id'), timer: @currentTime
    
    if @playing
      @playing = false
      @perform 'pause', args...
  
  updateTrackInfo: ->
    trackInfo = @widget.find('.time')
    duration  = $.jPlayer.convertTime(parseInt(@track.attr('data-length')))
    artist    = @track.find('.track-artist').text()
    title     = @track.find('.track-title').text()
    
    trackInfo.find('.current-time').text('00:00')
    trackInfo.find('.track-length').text(duration)
    trackInfo.find('.track-artist').text(artist)
    trackInfo.find('.track-title').text(title)
  
  change: (track, trackChange = false) ->
    @track = $ track
    
    if trackChange
      SC.trackEvent 'change-track', track: @track?.attr('data-track-id'), timer: @currentTime
    
    # Set the track so we can play it:
    file = @track.attr('data-attachment-url')
    @perform 'setMedia', mp3: file
    
    # If we're already playing, start playing the track we're switching to:
    if @playing
      @pause()
      @updateTrackInfo()
      @play()
    else
      @updateTrackInfo()

$ ->
  $('.download-release').click ->
    $('.player-track').each ->
      track = $ this
      SC.trackEvent 'download-track', track: track?.attr('data-track-id')
  
  if $.fn.raty?
    $('.star').parent().find('select.rating').hide()
    $('.star').raty
      starHalf: '<%= image_path "icons/star-half.png" %>'
      starOff:  '<%= image_path "icons/star-off.png" %>'
      starOn:   '<%= image_path "icons/star-on.png"  %>',
      half:  true,
      click: (stars, event) ->
        track = $(this).parent()
        
        SC.trackEvent 'rate-track', track: track?.attr('data-track-id'), stars: stars
        
        if stars
          score = stars * 2
          track.find('select.rating').val(score)
      score: ->
        track = $(this).parent()
        score = track.find('select.rating').val()
        if score
          score / 2
        else
          false
  
  if $.fn.jPlayer?
    player = new SC.Player($('.release-player'), $('.player-track'))
    
    # Click on playlist:
    $('.player-track-title').on    'click', -> player.change($(this).parent(), true)
    
    # Click on prev/next button:
    $('.release-player .left').on  'click', -> player.prev()
    $('.release-player .right').on 'click', -> player.next()
    
    # Click on play/pause button:
    $('.release-player .playpause').on 'click', ->
      button = $ this
      if player.isPlaying()
        player.pause()
        button.removeClass('playing')
              .addClass('paused')
      else
        player.play()
        button.removeClass('paused')
              .addClass('playing')
    