#= require jplayer/jquery.jplayer.min
#= require jquery.raty.min
#= require sc_track_event
#= require sc_waveform
#= require sc_player

$ ->
  $('.download-release').click ->
    $('.player-track').each ->
      track = $ this
      SC.trackEvent 'download-track', track: track?.attr('data-track-id')
  
  $('.player-track .download').click ->
    track = $(this).parent()
    SC.trackEvent 'download-track', track: track?.attr('data-track-id')
  
  if $.fn.raty?
    retina = window?.devicePixelRatio > 1
    
    $('.star').parent().find('select.rating').hide()
    $('.star').raty
      starHalf: if retina then '<%= image_path "icons/star-half@2x.png" %>' else '<%= image_path "icons/star-half.png" %>'
      starOff:  if retina then '<%= image_path "icons/star-off@2x.png" %>'  else '<%= image_path "icons/star-off@2x.png" %>'
      starOn:   if retina then '<%= image_path "icons/star-on@2x.png"  %>'  else '<%= image_path "icons/star-on@2x.png"  %>'
      half:     true
      click: (stars, event) ->
        track = $(this).parent()
        
        SC.trackEvent 'rate-track', track: track?.attr('data-track-id'), stars: stars
        
        if stars
          score = stars * 2
          track.find('select.rating').val(score)
      score: ->
        track = $(this).parent()
        score = track.find('select.rating').val()
        if score
          score / 2
        else
          false
  
  if $.fn.jPlayer?
    player = new SC.Player($('.release-player'), $('.player-track'))
    
    # Click on playlist:
    $('.player-track-title').on    'click', -> player.change($(this).parent(), true)
    
    # Click on prev/next button:
    $('.release-player .left').on  'click', -> player.prev()
    $('.release-player .right').on 'click', -> player.next()
    
    # Click on play/pause button:
    $('.release-player .playpause').on 'click', ->
      button = $ this
      if player.isPlaying()
        player.pause()
        button.removeClass('playing')
              .addClass('paused')
      else
        player.play()
        button.removeClass('paused')
              .addClass('playing')
       
    